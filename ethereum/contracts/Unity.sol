pragma solidity ^0.4.23;


import './Logger.sol';

contract Unity {
    Logger logger;

    // Define the structure of a land.
    struct Land {
        bytes2 district;
        uint document;
        uint landParcel;
        uint subaltern;
        address ownerAddress;
    }

    // Define the strcuture of an history entry.
    struct EntryHistory {
        address ownerAddress;
        uint timestamp;
    }

    // Define the structure of the IPFS hash.
    struct Multihash {
        bytes32 hash;
        uint8 hash_function;
        uint8 size;
    }

    // address of who creates the contract
    address public owner;

    // IPFS hash field.
    Multihash public _ipfs;

    // One landId can have multiple passed owners.
    EntryHistory[] public __history;

    Land public _land;

    address exOwner;

    // When the contract is deployed by truffle the account that deploys the contract became the owner,
    // in our case it will be the first account generated by ganache-cli (accounts[0]).
    constructor (address loggerAddress, bytes2 _district, uint _document, uint _landParcel, uint _subaltern, address _ownerAddress) public{
        owner = msg.sender;

        exOwner = _ownerAddress;

        logger = Logger(loggerAddress);

        _land = Land({
            district : _district,
            document : _document,
            landParcel : _landParcel,
            subaltern : _subaltern,
            ownerAddress : _ownerAddress
            });

        // Add history entry for this new land.
        EntryHistory memory myEntry = EntryHistory({
            ownerAddress : _ownerAddress,
            timestamp : block.timestamp
            });
        __history.push(myEntry);

        //        emit Add(_ownerAddress, _landParcel);
        //        _contractAddress, _district, _document, _landParcel, _subaltern, _ownerAddress
        logger.emitNewDeployEvent(address(this), _land.district, _land.document, _land.landParcel, _land.subaltern, _land.ownerAddress);
    }

    // Land transfer event, also use this tho synchronize with the backend.
    event Transfer(address indexed _from, address indexed _to, uint _landParcel);

    // Modifier to check if the address of the calling (from field in the transaction obj) is the owner.
    modifier isOwner(){
        require(msg.sender == _land.ownerAddress, "This land is not of the caller");
        _;
    }

    modifier isOwnerOrEx(){
        require(msg.sender == _land.ownerAddress || msg.sender == exOwner,
            "This land is not of the caller or the ex owner");
        _;
    }


    function setIPFS(bytes32 _hash, uint8 _hash_function, uint8 _size) public isOwnerOrEx {
        _ipfs = Multihash({
            hash : _hash,
            hash_function : _hash_function,
            size : _size
            });
    }

    function getIPFS() public view returns (bytes32, uint8, uint8){
        return (_ipfs.hash, _ipfs.hash_function, _ipfs.size);
    }

    // Caller (owner/anyone) can transfer a land to the provided buyer if the caller is the owner of the requested land.
    function transferLand(address _landBuyer, uint _landParcel, uint _subaltern) isOwner public returns (bool) {
        // If so, then the land ID is in owner's collection.

        // exOwner is used in order to set the IPFS when the land is transferred.
        exOwner = _land.ownerAddress;
        if (_land.landParcel == _landParcel && _land.subaltern == _subaltern) {
            _land.ownerAddress = _landBuyer;

            // Insert movement in the history mapping.
            EntryHistory memory myEntry = EntryHistory({
                ownerAddress : _landBuyer,
                timestamp : block.timestamp
                });
            __history.push(myEntry);

            // Inform the backend or who is subscribed to the event.
            //            emit Transfer(msg.sender, _landBuyer, _landParcel);
            logger.emitTransfer(address(this), _land.landParcel, _land.subaltern, _landBuyer, exOwner);
            return true;
        }
        return false;
    }


    // Get land details of an account.
    function getLand() public view returns (bytes2, uint, uint, uint, address) {
        return (
        _land.district,
        _land.document,
        _land.landParcel,
        _land.subaltern,
        _land.ownerAddress);

    }

    // Return all the movements for a specific land.
    function getHistory(uint _index) public view returns (address, uint) {
        return (
        __history[_index].ownerAddress,
        __history[_index].timestamp
        );
    }

    // Return number of movements for a specific land.
    function getNoOfEntries() public view returns (uint){
        return __history.length;
    }
}